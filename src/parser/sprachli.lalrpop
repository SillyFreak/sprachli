use crate::ast;

grammar;

pub SourceFile: ast::SourceFile<'input> = {
	<declarations:Declaration*> => ast::SourceFile { <> },
}

#[inline]
Variable: ast::Variable<'input> = {
	<mutable:"mut"?> <name:Identifier> => {
		let mutable = mutable.is_some();
		ast::Variable { <> }
	},
}

#[inline]
FnTrunk: ast::FnTrunk<'input> = {
	"(" <CommaSeparated<Variable>> ")" <Block>
	=> ast::FnTrunk::new(<>),
}

//////
// declarations

pub Declaration: ast::Declaration<'input> = {
	// Use
	<decl:FnDeclaration> => decl.into(),
	<decl:Struct> => decl.into(),
	// Mixin
	// Impl
}

#[inline]
Visibility: ast::Visibility = {
	=> ast::Visibility::Private,
	"pub" => ast::Visibility::Public,
}

pub FnDeclaration: ast::FnDeclaration<'input> = {
	<Visibility> "fn" <Identifier> <FnTrunk>
	=> ast::FnDeclaration::new(<>),
}

pub Struct: ast::Struct<'input> = {
	<Visibility> "struct" <Identifier> <StructMembers>
	=> ast::Struct::new(<>),
}

#[inline]
StructMembers: ast::StructMembers<'input> = {
	";" => ast::StructMembers::Empty,
	"(" <CommaSeparated<Identifier>> ")" ";" => ast::StructMembers::Positional(<>),
	"{" <CommaSeparated<Identifier>> "}" => ast::StructMembers::Named(<>),
};

//////
// statements

pub Statement: ast::Statement<'input> = {
	Declaration => ast::Statement::Declaration(<>),
	<Expression> ";" => ast::Statement::Expression(<>),
	<Jump> ";",
	<VariableDeclaration> ";",
	<Assignment> ";",
}

pub Jump: ast::Statement<'input> = {
	"return" <Expression?> => ast::Jump::new_return(<>).into(),
	"break" <Expression?> => ast::Jump::new_break(<>).into(),
	"continue" => ast::Jump::Continue.into(),
}

pub VariableDeclaration: ast::Statement<'input> = {
	"let" <Variable> <("=" <Expression>)?> => ast::VariableDeclaration::new(<>).into(),
}

pub Assignment: ast::Statement<'input> = {
	<Expression> "=" <Expression> => ast::Assignment::new(<>).into(),
}

//////
// expressions

pub Expression: ast::Expression<'input> = {
	// atomic expressions - either consist of one token, or are unambiguously delimited
	#[precedence(level="1")]
	Number => ast::Expression::Number(<>),
	Bool => ast::Expression::Bool(<>),
	String => ast::Expression::String(<>),
	Identifier => ast::Expression::Identifier(<>),
	Block => ast::Expression::Block(<>),
	Fn,
	Group,
	If,
	Loop,

	// paths
	// #[precedence(level="2")]

	// method calls
	// #[precedence(level="3")]

	// field expressions
	// #[precedence(level="4")]
	// #[assoc(side="left")]

	// Function calls, array indexing
	#[precedence(level="5")]
	<Expression> "(" <ActualParameters> ")" => ast::Call::new(<>).into(),

	// unary ! -
	#[precedence(level="6")]
	<UnaryOp> <Expression> => ast::Unary::new(<>).into(),
	
	// multiplicative * / %
	#[precedence(level="7")]
	#[assoc(side="left")]
	<Expression> <MultiplicativeOp> <Expression> => ast::Binary::new(<>).into(),

	// additive + -
	#[precedence(level="8")]
	#[assoc(side="left")]
	<Expression> <AdditiveOp> <Expression> => ast::Binary::new(<>).into(),

	// shift >> <<
	#[assoc(side="left")]
	#[precedence(level="9")]
	<Expression> <ShiftOp> <Expression> => ast::Binary::new(<>).into(),

	// bitwise &
	#[assoc(side="left")]
	#[precedence(level="10")]
	<Expression> <BitAndOp> <Expression> => ast::Binary::new(<>).into(),

	// bitwise ^
	#[assoc(side="left")]
	#[precedence(level="11")]
	<Expression> <BitXorOp> <Expression> => ast::Binary::new(<>).into(),

	// bitwise |
	#[assoc(side="left")]
	#[precedence(level="12")]
	<Expression> <BitOrOp> <Expression> => ast::Binary::new(<>).into(),

	// comparison == != < > <= >=
	#[precedence(level="13")]
	#[assoc(side="none")]
	<Expression> <ComparisonOp> <Expression> => ast::Binary::new(<>).into(),

	// short-circuiting logical &&
	// #[assoc(side="left")]
	// #[precedence(level="14")]
	// <Expression> <LogicalAndOp> <Expression> => ast::Binary::new(<>).into(),

	// short-circuiting logical ||
	// #[assoc(side="left")]
	// #[precedence(level="15")]
	// <Expression> <LogicalOrOp> <Expression> => ast::Binary::new(<>).into(),
}

#[inline]
ActualParameters: Vec<ast::Expression<'input>> = {
	CommaSeparated<Expression>
}

UnaryOp: ast::UnaryOperator = {
	"-" => ast::UnaryOperator::Negate,
	"!" => ast::UnaryOperator::Not,
}

MultiplicativeOp: ast::BinaryOperator = {
	"*" => ast::BinaryOperator::Multiply,
	"/" => ast::BinaryOperator::Divide,
	"%" => ast::BinaryOperator::Modulo,
}

AdditiveOp: ast::BinaryOperator = {
	"+" => ast::BinaryOperator::Add,
	"-" => ast::BinaryOperator::Subtract,
}

ShiftOp: ast::BinaryOperator = {
	">>" => ast::BinaryOperator::RightShift,
	"<<" => ast::BinaryOperator::LeftShift,
}

BitAndOp: ast::BinaryOperator = {
	"&" => ast::BinaryOperator::BitAnd,
}

BitXorOp: ast::BinaryOperator = {
	"^" => ast::BinaryOperator::BitXor,
}

BitOrOp: ast::BinaryOperator = {
	"|" => ast::BinaryOperator::BitOr,
}

ComparisonOp: ast::BinaryOperator = {
	"==" => ast::BinaryOperator::Equals,
	"!=" => ast::BinaryOperator::NotEquals,
	">" => ast::BinaryOperator::Greater,
	">=" => ast::BinaryOperator::GreaterEquals,
	"<" => ast::BinaryOperator::Less,
	"<=" => ast::BinaryOperator::LessEquals,
}

// LogicalAndOp: ast::BinaryOperator = {
// 	"&&" => ast::BinaryOperator::LogicalAnd,
// }

// LogicalOrOp: ast::BinaryOperator = {
// 	"||" => ast::BinaryOperator::LogicalOr,
// }

pub Block: ast::Block<'input> = {
	"{" <Statement*> <Expression?> "}" => ast::Block::new(<>),
}

pub Fn: ast::Expression<'input> = {
	"fn" <FnTrunk> => ast::Fn::new(<>).into(),
}

pub Group: ast::Expression<'input> = {
	"(" <Expression> ")",
}

pub If: ast::Expression<'input> = {
	<mut then_branches:(<ThenBranch> "else")*>
	<last_then_branch:ThenBranch>
	<else_branch:("else" <Block>)?>
	=> {
		then_branches.push(last_then_branch);
		ast::If::new(then_branches, else_branch).into()
	},
}

#[inline]
ThenBranch: (ast::Expression<'input>, ast::Block<'input>) = {
	"if" <Expression> <Block>
}

pub Loop: ast::Expression<'input> = {
	"loop" <Block> => ast::Loop::new(<>).into()
}

//////
// misc

#[inline]
CommaSeparated<T>: Vec<T> = { // (1)
	<mut v:(<T> ",")*> <e:T?> => { // (2)
		v.extend(e);
		v
	}
}

pub Identifier: &'input str = {
	IDENTIFIER,
}

pub Number: &'input str = {
	NUMBER,
}

pub Bool: bool = {
	"true" => true,
	"false" => false,
}

pub String: &'input str = {
	STRING,
}

match {
	// keywords must have priority over identifiers
	"pub", "fn", "struct",
	"return", "break", "continue",
	"let", "mut",
	"if", "else", "loop",
	"true", "false",
} else {
	r"[_a-zA-Z][_a-zA-Z0-9]*" => IDENTIFIER,
	r"[0-9]+" => NUMBER,
	r#""([^\\"]|\\[\\nrt"])*""# => STRING,

	r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
	r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
	r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
	_,
}