use crate::ast;

grammar;

pub SourceFile: ast::SourceFile<'input> = {
	<declarations:Declaration*> => ast::SourceFile { <> },
}

//////
// declarations

pub Declaration: ast::Declaration<'input> = {
	// Use
	<Fn> => ast::Declaration::Fn(<>),
	<Struct> => ast::Declaration::Struct(<>),
	// Mixin
	// Impl
}

Visibility: ast::Visibility = {
	=> ast::Visibility::Private,
	"pub" => ast::Visibility::Public,
}

pub Fn: ast::Fn<'input> = {
	<visibility:Visibility>
	"fn" <name:Identifier>
	"(" <formal_parameters:CommaSeparated<Identifier>> ")"
	<body:Block> => ast::Fn::new(<>),
}

pub Struct: ast::Struct<'input> = {
	<visibility:Visibility>
	"struct" <name:Identifier>
	<members:StructMembers> => ast::Struct::new(<>),
}

StructMembers: ast::StructMembers<'input> = {
	";" => ast::StructMembers::Empty,
	"(" <members:CommaSeparated<Identifier>> ")" ";" => ast::StructMembers::Positional(<>),
	"{" <members:CommaSeparated<Identifier>> "}" => ast::StructMembers::Named(<>),
};

//////
// statements

pub Statement: ast::Statement<'input> = {
	Declaration => ast::Statement::Declaration(<>),
	<Expression> ";" => ast::Statement::Expression(<>),
}

//////
// expressions

pub Expression: ast::Expression<'input> = {
	Equality,
}

pub Equality: ast::Expression<'input> = {
	<left:Equality> <operator:EqualityOp> <right:Comparison> => ast::Binary::new(<>).into(),
	Comparison,
}

EqualityOp: ast::BinaryOperator = {
	"==" => ast::BinaryOperator::Equals,
	"!=" => ast::BinaryOperator::NotEquals,
}

pub Comparison: ast::Expression<'input> = {
	<left:Comparison> <operator:ComparisonOp> <right:Additive> => ast::Binary::new(<>).into(),
	Additive,
}

ComparisonOp: ast::BinaryOperator = {
	">" => ast::BinaryOperator::Greater,
	">=" => ast::BinaryOperator::GreaterEquals,
	"<" => ast::BinaryOperator::Less,
	"<=" => ast::BinaryOperator::LessEquals,
}

pub Additive: ast::Expression<'input> = {
	<left:Additive> <operator:AdditiveOp> <right:Multiplicative> => ast::Binary::new(<>).into(),
	Multiplicative,
}

AdditiveOp: ast::BinaryOperator = {
	"+" => ast::BinaryOperator::Add,
	"-" => ast::BinaryOperator::Subtract,
}

pub Multiplicative: ast::Expression<'input> = {
	<left:Multiplicative> <operator:MultiplicativeOp> <right:Unary> => ast::Binary::new(<>).into(),
	Unary,
}

MultiplicativeOp: ast::BinaryOperator = {
	"*" => ast::BinaryOperator::Multiply,
	"/" => ast::BinaryOperator::Divide,
}

pub Unary: ast::Expression<'input> = {
	<operator:UnaryOp> <right:Unary> => ast::Unary::new(<>).into(),
	Call,
}

UnaryOp: ast::UnaryOperator = {
	"-" => ast::UnaryOperator::Negate,
	"!" => ast::UnaryOperator::Not,
}

pub Call: ast::Expression<'input> = {
	<function:Atomic> "(" <actual_parameters:CommaSeparated<Expression>> ")" => ast::Call::new(<>).into(),
	Atomic,
}

pub Atomic: ast::Expression<'input> = {
	Number => ast::Expression::Number(<>),
	String => ast::Expression::String(<>),
	Identifier => ast::Expression::Identifier(<>),
	Block => ast::Expression::Block(<>),
	If => ast::Expression::If(<>),
	"(" <Expression> ")",
}

pub Block: ast::Block<'input> = {
	"{" <statements:Statement*> <expression:Expression?> "}" => ast::Block::new(<>),
}

pub If: ast::If<'input> = {
	<mut then_branches:(<ThenBranch> "else")*>
	<last_then_branch:ThenBranch>
	<else_branch:("else" <Block>)?>
	=> {
		then_branches.push(last_then_branch);
		ast::If::new(then_branches, else_branch)
	},
}

ThenBranch: (ast::Expression<'input>, ast::Block<'input>) = {
	"if" <Expression> <Block>
}

//////
// misc

CommaSeparated<T>: Vec<T> = { // (1)
	<mut v:(<T> ",")*> <e:T?> => { // (2)
		v.extend(e);
		v
	}
}

pub Identifier: &'input str = {
	IDENTIFIER,
}

pub Number: &'input str = {
	NUMBER,
}

pub String: &'input str = {
	STRING,
}

match {
	// keywords must have priority over identifiers
	"pub", "fn", "struct", "if", "else",
} else {
	r"[_a-zA-Z][_a-zA-Z0-9]*" => IDENTIFIER,
	r"[0-9]+" => NUMBER,
	r#""([^\\"]|\\[\\nrt"])*""# => STRING,

	r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
	r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
	r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
	_,
}